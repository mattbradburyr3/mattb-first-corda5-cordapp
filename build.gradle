import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'org.jetbrains.kotlin.jvm'

    // (1)
    id 'net.corda.plugins.cordapp-cpb'
}

group 'org.example'
version '1.0-SNAPSHOT'

def javaVersion = VERSION_11

wrapper {
    gradleVersion = '7.2'
    distributionType = Wrapper.DistributionType.BIN
}

// (2)
cordapp {
    targetPlatformVersion platformVersion as Integer
    minimumPlatformVersion platformVersion as Integer
    workflow
            {
                name "Mattb-first-corda5-cordapp"
                versionId 1
                vendor "R3"
            }
}

// (3)
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions
            {
                allWarningsAsErrors = true
                languageVersion = '1.6'
                apiVersion = '1.6'
                jvmTarget = javaVersion
                javaParameters = true
                freeCompilerArgs += [
                        "-java-parameters",
                        "-Xjvm-default=all"
                ]
            }
}

// (4)
repositories {
    mavenLocal()
    mavenCentral()

    // Needed for kotlin osgi bundles for corda
    maven {
        url = "$artifactoryContextUrl/corda-dependencies"
    }
}

// (5)
dependencies {
    // We need to use kotlin stdlib built as an OSGi Bundle
    cordaProvided 'net.corda.kotlin:kotlin-stdlib-jdk8-osgi'
    cordaProvided platform("net.corda:corda-api:$cordaApiVersion")
    cordaProvided 'net.corda:corda-base'
    cordaProvided 'net.corda:corda-application'
    cordaProvided 'org.slf4j:slf4j-api'
}

test {
    useJUnitPlatform()
}